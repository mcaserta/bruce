plugins {
    id 'java-library'
    id 'org.sonarqube' version '5.0.0.4638'
    id 'jacoco'
    id 'maven-publish'
    id("com.diffplug.spotless") version "6.25.0"
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.bouncycastle:bcprov-jdk18on:1.77'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.10.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.10.2'
    testImplementation 'org.mockito:mockito-core:5.11.0'
}

group = 'com.github.mcaserta'
version = '1.0.6-SNAPSHOT'
description = 'An ergonomic wrapper around the JCA api'

java {
    sourceCompatibility = JavaVersion.VERSION_17
    withSourcesJar()
    withJavadocJar()
}

test {
    useJUnitPlatform()
}

tasks.named('compileJava') {
    options.encoding = 'UTF-8'
    options.javaModuleVersion = provider { project.version }
}

publishing {
    publications {
        myLibrary(MavenPublication) {
            from components.java
        }
    }
}

sonarqube {
    properties {
        property "sonar.projectKey", "mcaserta_bruce"
        property "sonar.organization", "mcaserta"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

jacocoTestReport {
    reports {
        xml.required = true
    }
}

spotless {
    java {
        googleJavaFormat('1.22.0').reflowLongStrings().formatJavadoc(true)
        removeUnusedImports()
        formatAnnotations()
    }
}
