plugins {
    id 'java-library'
    id 'org.sonarqube' version '3.2.0'
    id 'jacoco'
    id 'maven-publish'
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.bouncycastle:bcprov-jdk15on:1.68'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.7.1'
    testImplementation 'org.springframework.boot:spring-boot-starter-test:2.4.5'
}

group = 'com.mirkocaserta'
version = '1.0.0-SNAPSHOT'
description = 'Bruce'

java {
    sourceCompatibility = JavaVersion.VERSION_15
    withSourcesJar()
    withJavadocJar()
}

test {
    useJUnitPlatform()
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/mcaserta/bruce"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

sonarqube {
    properties {
        property "sonar.projectKey", "mcaserta_bruce"
        property "sonar.organization", "mcaserta"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}
